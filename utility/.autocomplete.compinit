#!/bin/zsh
emulate -L zsh -o extendedglob -o NO_shortloops -o warncreateglobal

# Workaround for https://github.com/zdharma/zinit/issues/366
[[ -v functions[.zinit-shade-off] ]] && .zinit-shade-off "${___mode:-load}"

unfunction compinit
autoload -Uz compinit

local REPLY
typeset -g _comp_dumpfile="${_comp_dumpfile:-${ZDOTDIR:-$HOME}/.zcompdump}"
local -a newer=( ${^fpath}/***/*(Ne:'[[ $REPLY -nt $_comp_dumpfile ]]:':Y1) )

if [[ -z "$functions[_autocomplete.is_glob]" || ! -e "$_comp_dumpfile" || -e $newer ]]; then

  # Decrease Oh My Zsh start-up time. See below.
  [[ -e "$ZSH_COMPDUMP" ]] && local -a omzdump=( ${(f)"$(<$ZSH_COMPDUMP)"} )

  [[ -e "$_comp_dumpfile" ]] && command rm -f "$_comp_dumpfile"
  compinit -C -d "$_comp_dumpfile"

  # Prevent Oh My Zsh from deleting comp dump file.
  [[ -e "$ZSH_COMPDUMP" ]] &&
    tee -a "$ZSH_COMPDUMP" &>/dev/null <<EOF
$omzdump[-2]
$omzdump[-1]
EOF

fi

_comp_options+=(
  globdots globstarshort listtypes numericglobsort
  NO_alwaystoend NO_caseglob NO_completealiases NO_globcomplete NO_listbeep
)

autoload -Uz .autocomplete.patch
local func; for func in _complete _default _description _expand _main_complete; do
  .autocomplete.patch $func
done
