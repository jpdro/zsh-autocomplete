#autoload

_autocomplete.recent_paths() {
  local -aU reply
  local ret=1

  if [[ $_comp_tags != *recent-dirs* ]] && _autocomplete.recent_dirs $words[CURRENT] &&
      .autocomplete.recent_paths.add recent-dirs 'recent directory' $reply; then
    _comp_tags+=' recent-dirs'
    ret=0
  fi

  [[ ! -v functions[_autocomplete.recent_files] ]] && return ret

  local mopts tmp1; zparseopts -E -a mopts '/=tmp1'
  [[ -z tmp1 ]] && return ret

  if [[ $_comp_tags != *recent-files* ]] && _autocomplete.recent_files $words[CURRENT] &&
      .autocomplete.recent_paths.add recent-files 'recent file' $reply; then
    _comp_tags+=' recent-files'
    ret=0
  fi

  return ret
}

.autocomplete.recent_paths.add() {
  local tag=$1
  local group_name=$2
  shift 2
  local -a paths=( "${(@b)${@:A}:#($PWD/|${words[CURRENT]:A})[^/]#}" )
  local -a display=( "${(@D)paths}" )

  _description -V $tag expl $group_name
  compadd "$expl[@]" -fQ -W '/' -d display -a paths
}

_autocomplete.recent_paths "$@"
