#!/bin/zsh

.autocomplete.recent-dirs.precmd() {
  emulate -L zsh -o extendedglob -o NO_shortloops -o warncreateglobal
  add-zsh-hook -d precmd .autocomplete.recent-dirs.precmd

  if [[ ( -v commands[fasd] || -v functions[fasd] ) && -v functions[_fasd_preexec] ]] &&
      zstyle -T :autocomplete: recent-files fasd; then

    _autocomplete.recent_files() {
      reply=( "$( fasd -flR $1 2> /dev/null )" )
    }
  fi

  if [[ -v functions[zshz] && -v functions[_zshz_precmd] ]] &&
      zstyle -T :autocomplete: recent-dirs zsh-z; then

    _autocomplete.recent_dirs() {
      reply=( "$( zshz --complete -l $1 2> /dev/null )" )
    }

  elif [[ -v commands[zoxide] && -v functions[_zoxide_hook] ]] &&
      zstyle -T :autocomplete: recent-dirs zoxide; then

    _autocomplete.recent_dirs() {
      reply=( "$( zoxide query --list $1 2> /dev/null )" )
    }

  elif [[ -v functions[_zlua] && -v functions[_zlua_precmd] ]] &&
      zstyle -T :autocomplete: recent-dirs z.lua; then

    _autocomplete.recent_dirs() {
      reply=( "${(@)${(f)$( _zlua --complete $1 2> /dev/null )}##[[:digit:].]##[[:space:]]##}" )
    }

  elif [[ -v functions[_z] && -v functions[_z_precmd] ]] &&
      zstyle -T :autocomplete: recent-dirs z.sh; then

    _autocomplete.recent_dirs() {
      reply=( "${(@)${(f)$( _z -l $1 2>&1 )}##(common:|[[:digit:]]##)[[:space:]]##}" )
    }

  elif [[ -v commands[autojump] && -v AUTOJUMP_SOURCED ]] &&
      zstyle -T :autocomplete: recent-dirs autojump; then

    _autocomplete.recent_dirs() {
      reply=( "${(@)${(f)$( autojump --complete $1 2> /dev/null )}##${1}__[[:digit:]]__}" )
    }

  elif [[ ( -v commands[fasd] || -v functions[fasd] ) && -v functions[_fasd_preexec] ]] &&
      zstyle -T :autocomplete: recent-dirs fasd; then

    _autocomplete.recent_dirs() {
      reply=( "$( fasd -dlR $1 2> /dev/null )" )
    }

  else
    zstyle ':chpwd:*' recent-dirs-max '0'
    autoload -Uz cdr chpwd_recent_dirs
    add-zsh-hook chpwd chpwd_recent_dirs

    _autocomplete.recent_dirs() {
      cdr -r
      [[ "$1:a" == "$1" ]] && reply=( "${(@M)reply:#$1*}" )
      (( $#reply > 0 ))
    }
  fi

  autoload -Uz .autocomplete.patch
  .autocomplete.patch _path_files
}

emulate -L zsh -o extendedglob -o NO_shortloops -o warncreateglobal
add-zsh-hook precmd .autocomplete.recent-dirs.precmd
