#!/bin/zsh

.autocomplete.config.precmd() {
  emulate -L zsh -o extendedglob -o NO_shortloops -o warncreateglobal
  add-zsh-hook -d precmd .autocomplete.config.precmd

  zmodload zsh/zutil  # `zstyle` builtin

  # Remove incompatible styles.
  zstyle -d ':completion:*' format
  zstyle -d ':completion:*:descriptions' format
  zstyle -d ':completion:*' group-name
  zstyle -d ':completion:*:functions' ignored-patterns
  zstyle -d '*' single-ignored
  zstyle -d ':completion:*' special-dirs

  zstyle ':completion:*' completer _autocomplete.old_list _expand _complete:-strict \
    _complete _correct _ignored _autocomplete.history_words
  zstyle ':completion:list-expand:*' completer _expand \
    _complete _correct _ignored _autocomplete.history_words
  zstyle ':completion:history-*search*:*' completer _autocomplete.history_lines

  zstyle ':completion:*' add-space true
  zstyle ':completion:*' list-packed true
  zstyle ':completion:*' use-cache true

  zstyle -e ':completion:*:options' list-grouped '
    reply=( true ); [[ -z $_autocomplete__punct ]] || reply=( false )'
  zstyle ':completion:*' list-separator ''
  zstyle ':completion:*' prefix-needed 'false'

  zstyle ':completion:*' tag-order '*'
  zstyle ':completion:*:(approximate|correct):*' tag-order '! original' '-'
  zstyle ':completion:*:expand:*' tag-order '! all-expansions original' '-'

  zstyle ':completion:*:complete-strict:*' matcher-list ''
  zstyle -e ':completion:*:complete-strict:*' ignored-patterns '
    case $_autocomplete__punct in
      -) reply=( "${(b)_autocomplete__head}([^-]*|--*)" ) ;;
      --) reply=( "${(b)_autocomplete__head}^(--)*" ) ;;
      +) reply=( "${(b)_autocomplete__head}[^+]*" ) ;;
      *) reply=( "${(b)_autocomplete__head}[^[:alnum:]]*" ) ;;
    esac'

  zstyle ':completion:*:complete:*' matcher-list '
    l:|=**' '+ r:|?=** m:{[:lower:][:upper:]-_}={[:upper:][:lower:]_-}'
  zstyle -e ':completion:*:complete:*' ignored-patterns '
    case $_autocomplete__punct in
      .) reply=( "${(b)_autocomplete__head}[^.[:alnum:]]*" ) ;;
      *) reply=( "${(b)_autocomplete__head}[^[:alnum:]]*" ) ;;
    esac'
  zstyle ':completion:list-expand:complete:*' ignored-patterns ''

  zstyle ':completion:list-expand:*' extra-verbose true

  zstyle ':completion:*:history-words' ignore-line current
  zstyle ':completion:*:history-words' ignored-patterns '?'
  zstyle ':completion:*:recent-(dirs|files)' ignored-patterns '?'
  zstyle ':completion:*:unambiguous' ignored-patterns '?'
  zstyle ':completion:*:(alias-expansions|requoted|unambiguous)' ignore-line current

  zstyle -e ':completion:*' glob 'reply=( "true" ) && _autocomplete.is_glob || reply=( "false" )'
  zstyle ':completion:*' expand prefix suffix
  zstyle ':completion:*' list-suffixes true
  zstyle ':completion:*' path-completion true

  zstyle ':completion:*' menu 'yes select=long-list'
  zstyle ':completion:list-expand:*' menu 'yes select'

  zstyle -t ":autocomplete:tab:" insert-unambiguous &&
    zstyle ':completion:*' show-ambiguity '07'

  if zstyle -t :autocomplete: groups always; then
    zstyle ':completion:*' group-name ''
    zstyle ':completion:*:descriptions' format '%F{blue}%d:%f'
  fi

  zstyle ':completion:list-expand:*' group-name ''
  zstyle ':completion:list-expand:*:descriptions' format '%F{blue}%d:%f'

  zstyle ':completion:*' list-dirs-first true
  zstyle ':completion:*:(|*-)directories(-*|)' group-name directories
  zstyle ':completion:*' group-order directories

  zstyle ':completion:*:infos' format '%F{yellow}%d%f'
  zstyle ':completion:*:messages' format '%F{red}%d%f'
  zstyle ':completion:*:warnings' format '%F{yellow}%d%f'
  zstyle ':completion:*:errors' format '%F{red}%d%f'
  zstyle ':completion:*' auto-description '%d'

  zstyle ':completion:*:(alias-expansions|requoted|unambiguous)'  group-name ''
  zstyle ':completion:*:(alias-expansions|requoted|unambiguous)' format \
    '%F{green}%d:  %F{blue}⇤ to insert%f'
  zstyle ':completion:list-expand:*:(alias-expansions|requoted|unambiguous)' format \
    '%F{green}%d:%f'

  zstyle ':completion:*:history-lines' group-name ''
  zstyle ':completion:*:history-lines' format \
    '%F{green}%d:  %F{blue}⇤ to insert  ↑ or ⇞ for more%f'
  zstyle ':completion:list-expand:*:history-lines' format '%F{green}%d:%f'
  zstyle ':completion:history-*search*:*:history-lines' format ''
}

emulate -L zsh -o extendedglob -o NO_shortloops -o warncreateglobal
add-zsh-hook precmd .autocomplete.config.precmd
