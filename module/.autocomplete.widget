#!/bin/zsh
emulate -L zsh -o noshortloops -o warncreateglobal -o extendedglob

export ZSH_AUTOSUGGEST_MANUAL_REBIND=1

add-zsh-hook precmd .autocomplete.widget.precmd

.autocomplete.widget.precmd() {
  emulate -L zsh -o noshortloops -o warncreateglobal -o extendedglob
  add-zsh-hook -d precmd .autocomplete.widget.precmd

  local tab_style
  zstyle -s ":autocomplete:tab:" widget-style tab_style || tab_style='complete-word'
  zstyle ":autocomplete:tab:*" widget-style $tab_style
  _autocomplete.widget.new menu-complete _menu_complete $tab_style

  local backtab_style=$tab_style
  [[ $tab_style == menu-complete ]] && backtab_style=reverse-menu-complete
  _autocomplete.widget.new menu-complete _reverse_menu_complete $backtab_style

  .autocomplete.widget.new menu-complete menu-complete
  .autocomplete.widget.new menu-complete reverse-menu-complete

  _autocomplete.widget.new list-expand list-expand menu-select

  _autocomplete.widget.new history-search _history_search menu-select
  .autocomplete.widget.new history-search history-search-backward
  .autocomplete.widget.new history-search history-search-forward
  .autocomplete.widget.new history-search history-incremental-search-backward
  .autocomplete.widget.new history-search history-incremental-search-forward

  .autocomplete.widget.new line-or-search up-line-or-search
  .autocomplete.widget.new line-or-search down-line-or-search

  .autocomplete.widget.new line-or-select down-line-or-select
  .autocomplete.widget.new line-or-select up-line-or-select

  if [[ -v functions[_zsh_autosuggest_bind_widgets] ]]; then
    [[ -v ZSH_AUTOSUGGEST_IGNORE_WIDGETS ]] && ZSH_AUTOSUGGEST_IGNORE_WIDGETS+=(
      prompt_\*
      user:_zsh_highlight_widget_\*-zle-line-finish
    )
    [[ -v ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS ]] && ZSH_AUTOSUGGEST_PARTIAL_ACCEPT_WIDGETS+=(
      forward-char
      vi-forward-char
    )
    _zsh_autosuggest_bind_widgets
  fi
}

_autocomplete.widget.new() {
  zle -C $2 $3 _autocomplete.$1
}

.autocomplete.widget.new() {
  local func=.autocomplete.$1
  autoload -Uz $func
  zle -N $2 $func
}
